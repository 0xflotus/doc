apply plugin: "org.ajoberstar.git-publish"

def docsDir = file("$buildDir/ghpages-docs")
def replaceCurrentDocs = project.hasProperty('replaceCurrentDocs')

task prepareDocsForUploadToGhPages(type: Copy) {
	dependsOn 'aggregateJavadocs', ':asciidoctor'
	outputs.dir docsDir

	includeEmptyDirs = false
}

task createCurrentDocsFolder(type: Copy) {
	dependsOn prepareDocsForUploadToGhPages
	outputs.dir "${docsDir}/current"
	onlyIf { replaceCurrentDocs }

	from "${docsDir}/${docsVersion}"
	into "${docsDir}/current"
}

gitPublish {
	repoUri = 'https://github.com/junit-team/junit5.git'
	branch = 'gh-pages'

	contents {
		from docsDir
		into "docs"
	}

	preserve {
		include '**/*'
		exclude "docs/${docsVersion}/**"
		if (replaceCurrentDocs) {
			exclude "docs/current/**"
		}
	}
}

gitPublishCommit.dependsOn(prepareDocsForUploadToGhPages, createCurrentDocsFolder)
